;
; Basic LCD routines for Game Boy
;
; Copyright 2018, 2020 Damian Yerrick
; 
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
include "src/hardware.inc"
include "src/global.inc"

def USE_NAM equ 1  ; used by SGB driver
def USE_GBC equ 1
def GBMONO_GREEN equ 0

section "irqvars",HRAM
; Used for bankswitching BG CHR RAM
vblank_lcdc_value:: ds 1

; The display list at $CE00-$CE9F gets DMA'd to OAM after every frame
; in which sprites moved.  Also called "shadow OAM".
section "ram_ppuclear",WRAM0,ALIGN[8]
SOAM:: ds 160
; How much of the display list is used; must be in same page as SOAM
oam_used:: ds 1
nmis:: ds 1

; A subroutine 7 bytes or smaller should go in its own section
; in order to fit between unused RST/IRQ handlers
SECTION "wnvblank", ROM0
;;
; Busy-wait for being out of vblank.  Use this for game loop timing
; if interrupts aren't in use yet.
wait_not_vblank::
  ldh a, [rLY]
  cp 144
  jr nc, wait_not_vblank
  ret

; Move some of the vblank handler into the 8-byte area
SECTION "rst40", ROM0[$0040]
  push af
  ld a,[nmis]
  inc a
  jp vblank_handler_part2

SECTION "rom_ppuclear", ROM0
vblank_handler_part2::
  ld [nmis],a
  ld a,[vblank_lcdc_value]
  ldh [rLCDC],a
  pop af
  reti

;;
; Busy-waits for blanking and turns off rendering.
;
; The Game Boy PPU halts entirely when rendering is off.  Stopping
; the signal outside vblank confuses the circuitry in the LCD panel,
; causing it to get stuck on a scanline.  This stuck state is the
; same as the dark horizontal line when you turn off the Game Boy.
;
; Turning rendering on, by contrast, can be done at any time and
; is done by writing the nametable base addresses and sprite size
; to rLCDC with bit 7 set to true.
; @return A = 0
lcd_off::
  call busy_wait_vblank

  ; Use a RMW instruction to turn off only bit 7
  xor a
  ldh [rLCDC],a
  ret

;;
; Waits for the vblank ISR to increment the count of vertical blanks.
; Will lock up if DI, vblank IRQ off, or LCD off.
; Clobbers A, HL
wait_vblank_irq::
  ld hl,nmis
  ld a,[hl]
  ; there's a slight risk of waiting 2 vblanks at some rare timing
.loop:
  halt
  cp [hl]
  jr z,.loop
  ret

;;
; Waits for forced blank (rLCDC bit 7 clear) or vertical blank
; (rLY >= 144).  Caution: this is a "room heater", best used before
; clearing rLCDC bit 7.
busy_wait_vblank::
  ; If rLCDC bit 7 already clear, we're already in forced blanking
  ldh a,[rLCDC]
  rlca
  ret nc

  ; Otherwise, wait for LY to become 144 through 152.
  ; Most of line 153 is prerender, during which LY reads back as 0.
.wait:
  ldh a, [rLY]
  cp 144
  jr c, .wait
  ret

;;
; Moves sprites in the display list from SOAM+[oam_used] through
; SOAM+$9C offscreen by setting their Y coordinate to 0, which is
; completely above the screen top (16).
lcd_clear_oam::
  ; Destination address in shadow OAM
  ld hl,oam_used
  ld a,[hl]
  and $FC
  ld l,a

  ; iteration count
  rrca
  rrca
  add 256 - 40
  ld c,a

  xor a
.rowloop:
  ld [hl+],a
  inc l
  inc l
  inc l
  inc c
  jr nz, .rowloop
  ret

if USE_NAM

load_full_nam::
  ld bc,256*20+18
  fallthrough load_nam
;;
; Copies a B column by C row tilemap from HL to screen at DE.
load_nam::
  push bc
  push de
  .byteloop:
    ld a,[hl+]
    ld [de],a
    inc de
    dec b
    jr nz,.byteloop

  ; Move to next screen row
  pop de
  ld a,32
  add e
  ld e,a
  jr nc,.no_inc_d
    inc d
  .no_inc_d:

  ; Restore width; do more rows remain?
  pop bc
  dec c
  jr nz,load_nam
  ret

endc

if USE_GBC

;;
; Emulates mono palette feature on Game Boy Color.
; Call this only during blanking.
set_obp1::
  ldh [rOBP1],a
  rra
  rra
  ld bc,$8A00 + low(rOCPS)
  jr set_gbc_mono_palette

;;
; Emulates mono palette feature on Game Boy Color.
; Call this only during blanking.
set_obp0::
  ldh [rOBP0],a
  ld bc,$8200 + low(rOCPS)
  rra
  rra
  jr set_gbc_mono_palette

;;
; Emulates mono palette feature on Game Boy Color.
; Call this only during blanking.
set_bgp::
  ldh [rBGP],a
  ld bc,$8000 + low(rBCPS)
  fallthrough set_gbc_mono_palette

;;
; Emulates mono palette setting on Game Boy Color.
; @param A BGP or OBP0 value
; @param B offset into palette memory (0, 8, 16, ..., 56) plus $80
; @param C palette port to write: LOW(rBCPS) or LOW(rOCPS)
; @return AEHL clobbered, B=0, C increased by 1, D unchanged
set_gbc_mono_palette::
  rlca
  ld e,a
  ld a,b  ; Regmap now: E=BGP<<1, A=palette offset, C=address port
  ldh [$FF00+c],a
  inc c   ; C=data port
  rra  ; Count remaining colors
  or %11111100
  ld b, a
  ld h,high(gbmonopalette)
  ld a, e
  ; Regmap now: B=negative of remaining colors (-4 BGP or -3 OBPx),
  ;   C=data port address, A=E=BGP value rlc 1,
  ;   HL=pointer to start of palette
.loop:
    and %00000110
    or low(gbmonopalette)
    ld l,a  ; now L points to this color so stuff it into the palette
    ld a,[hl+]
    ldh [$FF00+c],a
    ld a,[hl-]
    ldh [$FF00+c],a
    ld a, e  ; move to next bitfield of BGP
    rrca
    rrca
    ld e, a
    inc b
    jr nz,.loop

  ; Restore BGP value
  rrca
  ret

;;
; Loads native GBC palette data.
; @param A offset into palette memory (0, 8, 16, ..., 56) plus $80
; @param B number of bytes to copy: 8 times number of subpalettes
; @param C palette port to write: LOW(rBCPS) or LOW(rOCPS)
; @param HL data source
; @return HL at end, C increased by 1, B = 0, DE unchanged
set_gbc_palette::
  ldh [$FF00+c],a
  inc c
  .loop:
    ld a,[hl+]
    ldh [$FF00+c],a
    dec b
    jr nz,.loop
  ret

;;
; Turns off rendering and clears the attribute plane of GBC tilemaps.
; @return A=0
clear_gbc_attr::
  call lcd_off
  ld a,[hw_capability]
  and $80
  ret z
  rlca
  ldh [rVBK],a
  ld de,_SCRN0
  ld bc,2048
  ld h, e
  call memset
  xor a
  ldh [rVBK],a
.not_gbc:
  ret

section "GBMONOPALETTE", ROM0, ALIGN[3]
gbmonopalette::
  if GBMONO_GREEN
  drgb $E8F840
  drgb $90C040
  drgb $408040
  drgb $004040
  else
  drgb $F8F8F8
  drgb $A8A8A8
  drgb $585858
  drgb $000000
  endc

endc
